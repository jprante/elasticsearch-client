apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    wagon
}

dependencies {
    wagon "org.apache.maven.wagon:wagon-ssh:${property('wagon.version')}"
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

task xbibUpload(type: Upload) {
    group = 'publish'
    configuration = configurations.archives
    uploadDescriptor = true
    repositories {
        if (project.hasProperty('xbibUsername')) {
            mavenDeployer {
                configuration = configurations.wagon
                repository(url: uri(project.property('xbibUrl'))) {
                    authentication(userName: xbibUsername, privateKey: xbibPrivateKey)
                }
            }
        }
    }
}

task sonatypeUpload(type: Upload) {
    group = 'publish'
    configuration = configurations.archives
    uploadDescriptor = true
    repositories {
        if (project.hasProperty('ossrhUsername')) {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: uri(ossrhReleaseUrl)) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: uri(ossrhSnapshotUrl)) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    groupId project.group
                    artifactId project.name
                    version project.version
                    name project.name
                    description 'Elasticsearch client'
                    packaging 'jar'
                    inceptionYear '2018'
                    url 'https://github.com/jprante/elasticsearch-client'
                    organization {
                        name 'xbib'
                        url 'http://xbib.org'
                    }
                    developers {
                        developer {
                            id 'jprante'
                            name 'JÃ¶rg Prante'
                            email 'joergprante@gmail.com'
                            url 'https://github.com/jprante'
                        }
                    }
                    scm {
                        url 'https://github.com/jprante/elasticsearch-client'
                        connection 'scm:git:git@github.com/jprante/elasticsearch-client.git'
                        developerConnection 'scm:git:git@github.com/jprante/elasticsearch-client.git'
                    }
                    licenses {
                        license {
                            name 'Apache License Version 2'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "org.xbib"
}
